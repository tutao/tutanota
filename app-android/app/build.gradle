apply plugin: 'com.android.application'

android {
	compileSdkVersion 31
	defaultConfig {
		applicationId "de.tutao.tutanota"
		minSdkVersion 23
		targetSdkVersion 30
		versionCode 396000
		versionName "3.96.0"
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

		javaCompileOptions {
			annotationProcessorOptions {
				arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
			}
		}
	}
	signingConfigs {
		release {
			// Provide non-empty placeholders because otherwise configuration will braek even in debug.
			storeFile file(System.getenv("APK_SIGN_STORE") ?: 'EMPTY')
			storePassword System.getenv("APK_SIGN_STORE_PASS" ?: 'EMPTY')
			keyAlias System.getenv("APK_SIGN_ALIAS" ?: 'EMPTY')
			keyPassword System.getenv("APK_SIGN_KEY_PASS" ?: 'EMPTY')

			enableV1Signing = true
			enableV2Signing = true
		}
	}
	flavorDimensions 'releaseType'
	productFlavors {
		tutao {
			signingConfig signingConfigs.release
		}
		fdroid {
		}
	}
	buildTypes {
		def distPath = "\"file:///android_asset/tutanota/dist/index-app.html\""
		debug {
			manifestPlaceholders = [contentProviderAuthority: 'de.tutao.fileprovider.debug']
			buildConfigField 'String', 'RES_ADDRESS', "\"file:///android_asset/tutanota/index-app.html\""
			applicationIdSuffix ".debug"
		}
		debugDist {
			initWith debug
			manifestPlaceholders = [contentProviderAuthority: 'de.tutao.fileprovider.debugdist']
			buildConfigField 'String', 'RES_ADDRESS', distPath
			applicationIdSuffix ".debugdist"
		}
		releaseTest {
			initWith release
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			manifestPlaceholders = [contentProviderAuthority: 'de.tutao.fileprovider.test']
			buildConfigField 'String', 'RES_ADDRESS', distPath
			applicationIdSuffix ".test"
		}
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			manifestPlaceholders = [contentProviderAuthority: 'de.tutao.fileprovider']
			buildConfigField 'String', 'RES_ADDRESS', distPath
		}
	}

	applicationVariants.all { variant ->
		variant.outputs.all { output ->
			def flavor = variant.productFlavors[0].name
			outputFileName = "tutanota-$flavor-${variant.buildType.name}-${variant.versionName}.apk"
		}
	}

	buildTypes.each {
		it.buildConfigField 'String', 'FILE_PROVIDER_AUTHORITY', '"' + it.manifestPlaceholders['contentProviderAuthority'] + '"'
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/ASL2.0'
	}

	lintOptions {
		disable 'MissingTranslation'
	}
}

task copyResourcesToClasses(type: Copy) {
	from "${projectDir}/src/main/assets/tutanota/test/AlarmCompatibilityTest.json"
	into "${buildDir}/intermediates/classes/test/debug/resources"
}

preBuild.dependsOn copyResourcesToClasses

tasks.withType(Test) {
	testLogging {
		exceptionFormat "full"
		events "started", "skipped", "passed", "failed"
		showStandardStreams true
	}
}

dependencies {
	def room_version = "2.4.2"
	def lifecycle_version = "2.4.1"
	def activity_version = "1.4.0"


	implementation fileTree(dir: 'libs', include: ['*.jar'])
	androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
	// Important: cannot be updated without additional measures as Android 6 and 7 do not have Java 9
	implementation 'commons-io:commons-io:2.5'
	implementation 'org.jdeferred:jdeferred-core:1.2.4'
	implementation 'androidx.core:core:1.7.0'
	implementation "androidx.activity:activity:$activity_version"
	implementation "androidx.biometric:biometric:1.1.0"

	implementation "androidx.room:room-runtime:$room_version"
	// For Kotlin use kapt instead of annotationProcessor
	annotationProcessor "androidx.room:room-compiler:$room_version"

	implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"


	testImplementation 'junit:junit:4.13.2'
	testImplementation "org.robolectric:robolectric:4.6.1"
	testImplementation "org.mockito:mockito-core:4.3.0"
	androidTestImplementation 'org.codehaus.jackson:jackson-mapper-asl:1.9.2'
}
